// SPDX-License-Identifier: MIT
pragma solidity ^0.8.34;

import {Test} from "forge-std/Test.sol";
import {Vm} from "forge-std/Vm.sol";
import {ClanWorld} from "../src/ClanWorld.sol";
import {
    ClanWorldConstants,
    ClanState,
    ClansmanState,
    BanditState,
    ActionType,
    StatusCode,
    Clan,
    ClanFullView,
    Mission,
    ClanOrder,
    WithdrawResourcesData,
    OrderResult
} from "../src/IClanWorld.sol";

contract BanditAttackHarness is ClanWorld {
    function forceAttackingBandit(uint8 region, uint32 strength, uint32 targetClanId) external returns (uint32 id) {
        id = _spawnBandit(region, strength);
        _bandits[id].state = BanditState.Attacking;
        _bandits[id].targetClanId = targetClanId;
    }

    function forceBanditAttackAttempt(uint32 banditId, uint32 targetClanId) external {
        _bandits[banditId].state = BanditState.Attacking;
        _bandits[banditId].targetClanId = targetClanId;
        _bandits[banditId].tickEnteredState = this.getWorldState().currentTick;
    }

    function setWallLevel(uint32 clanId, uint8 wallLevel) external {
        _clans[clanId].wallLevel = wallLevel;
    }

    function setStarvationStartsAt(uint32 clanId, uint64 tick) external {
        _clans[clanId].starvationStartsAtTick = tick;
    }

    function setClanUpkeepState(uint32 clanId, uint64 lastSettledTick, uint256 vaultWheat, uint256 vaultFish) external {
        Clan storage clan = _clans[clanId];
        clan.lastSettledTick = lastSettledTick;
        clan.vaultWheat = vaultWheat;
        clan.vaultFish = vaultFish;
        clan.starvationStartsAtTick = 0;
    }

    function setLivingClansmenForTest(uint32 clanId, uint8 livingClansmen) external {
        _clans[clanId].livingClansmen = livingClansmen;
    }

    function blockBanditSpawnsForTest() external {
        uint64 currentTick = this.getWorldState().currentTick;
        for (uint8 region = ClanWorldConstants.REGION_FOREST; region <= ClanWorldConstants.REGION_DEEP_SEA; region++) {
            _banditSpawnByRegion[region].lastSpawnTick = currentTick;
        }
    }

    function blueprintUnit() external pure returns (uint256) {
        return BLUEPRINT_UNIT;
    }

    function setBanditStrength(uint32 banditId, uint32 strength) external {
        _bandits[banditId].strength = strength;
    }

    function setBanditCarry(uint32 banditId, uint256 wood, uint256 wheat, uint256 fish, uint256 iron, uint256 gold)
        external
    {
        _bandits[banditId].carryWood = wood;
        _bandits[banditId].carryWheat = wheat;
        _bandits[banditId].carryFish = fish;
        _bandits[banditId].carryIron = iron;
        _bandits[banditId].carryGold = gold;
    }

    function defenseRoll(bytes32 tickSeed, uint32 banditId, uint32 clansmanId) external pure returns (uint32) {
        return _clansmanDefenseDamageRoll(tickSeed, banditId, clansmanId);
    }
}

contract BanditAttackResolutionTest is Test {
    BanditAttackHarness world;
    address elder = address(0xA1);

    event BanditAttackResolved(
        uint32 indexed banditId,
        uint32 indexed targetClanId,
        bool defended,
        uint16 attackPower,
        uint16 totalDefense,
        uint16 wallLevelAfter,
        uint256 stolenWood,
        uint256 stolenIron,
        uint256 stolenWheat,
        uint256 stolenFish,
        uint64 atTick
    );
    event BanditDefeated(uint32 indexed banditId, uint32 indexed targetClanId, uint64 atTick);
    event BlueprintEarned(uint32 indexed clanId, uint32 indexed banditId, uint256 amount, uint64 tick);
    event BanditTargetDied(uint32 indexed banditId, uint32 indexed deadClanId, uint64 tick);
    event LootDistributed(
        uint32 indexed banditId,
        uint32[] clanIdsRewarded,
        uint256 perClanWood,
        uint256 perClanWheat,
        uint256 perClanFish,
        uint256 perClanIron,
        uint256 perClanGold,
        uint256 burnedWood,
        uint256 burnedWheat,
        uint256 burnedFish,
        uint256 burnedIron,
        uint256 burnedGold
    );

    function setUp() public {
        world = new BanditAttackHarness();
    }

    function _mintClan() internal returns (uint32 clanId) {
        vm.prank(elder);
        (clanId,) = world.mintClan(elder);
    }

    function _csId(uint32 clanId, uint256 index) internal view returns (uint32) {
        return world.getClanFullView(clanId).clansmen[index].clansman.clansman.clansmanId;
    }

    function _defendOrders(uint32 clanId, uint256 count) internal view returns (ClanOrder[] memory orders) {
        Clan memory clan = world.getClan(clanId);
        return _defendTargetOrders(clanId, clanId, clan.baseRegion, count);
    }

    function _defendTargetOrders(uint32 clanId, uint32 targetClanId, uint8 targetRegion, uint256 count)
        internal
        view
        returns (ClanOrder[] memory orders)
    {
        orders = new ClanOrder[](count);
        for (uint256 i = 0; i < count; i++) {
            orders[i] = ClanOrder({
                clansmanId: _csId(clanId, i),
                gotoRegion: targetRegion,
                action: ActionType.DefendBase,
                targetClanId: targetClanId,
                marketToken: address(0),
                marketAmount: 0,
                maxGoldIn: 0,
                withdrawResources: WithdrawResourcesData({wood: 0, iron: 0, wheat: 0, fish: 0})
            });
        }
    }

    function _submitDefenders(uint32 clanId, uint256 count) internal {
        _submitTargetDefenders(clanId, clanId, count);
    }

    function _submitTargetDefenders(uint32 defenderClanId, uint32 targetClanId, uint256 count)
        internal
        returns (uint64 maxExecutesAtTick)
    {
        Clan memory targetClan = world.getClan(targetClanId);
        ClanOrder[] memory orders = _defendTargetOrders(defenderClanId, targetClanId, targetClan.baseRegion, count);
        vm.prank(elder);
        OrderResult[] memory results = world.submitClanOrders(defenderClanId, orders);
        for (uint256 i = 0; i < count; i++) {
            assertEq(uint8(results[i].status), uint8(StatusCode.OK), "defender order status");
            Mission memory mission = world.getActiveMission(orders[i].clansmanId);
            if (mission.executesAtTick > maxExecutesAtTick) {
                maxExecutesAtTick = mission.executesAtTick;
            }
        }
    }

    function _advanceTick() internal {
        vm.warp(block.timestamp + ClanWorldConstants.HEARTBEAT_INTERVAL_SECONDS);
        world.heartbeat();
    }

    function _advanceUntilAfter(uint64 tick) internal {
        while (world.getWorldState().currentTick <= tick) {
            _advanceTick();
        }
    }

    function _advanceUntil(uint64 tick) internal {
        while (world.getWorldState().currentTick < tick) {
            _advanceTick();
        }
    }

    function _assertBanditTargetDiedLog(
        Vm.Log[] memory logs,
        uint32 expectedBanditId,
        uint32 expectedDeadClanId,
        uint64 expectedTick
    ) internal {
        bytes32 eventSig = keccak256("BanditTargetDied(uint32,uint32,uint64)");
        bytes32 expectedBanditTopic = bytes32(uint256(expectedBanditId));
        bytes32 expectedClanTopic = bytes32(uint256(expectedDeadClanId));
        for (uint256 i = 0; i < logs.length; i++) {
            if (
                logs[i].topics.length == 3 && logs[i].topics[0] == eventSig && logs[i].topics[1] == expectedBanditTopic
                    && logs[i].topics[2] == expectedClanTopic
            ) {
                uint64 tick = abi.decode(logs[i].data, (uint64));
                if (tick == expectedTick) return;
            }
        }
        fail("expected BanditTargetDied log");
    }

    function _assertBanditAttackResolvedLog(Vm.Log[] memory logs, uint32 expectedBanditId, uint32 expectedTargetClanId)
        internal
    {
        bytes32 eventSig = keccak256(
            "BanditAttackResolved(uint32,uint32,bool,uint16,uint16,uint16,uint256,uint256,uint256,uint256,uint64)"
        );
        bytes32 expectedBanditTopic = bytes32(uint256(expectedBanditId));
        bytes32 expectedClanTopic = bytes32(uint256(expectedTargetClanId));
        for (uint256 i = 0; i < logs.length; i++) {
            if (
                logs[i].topics.length == 3 && logs[i].topics[0] == eventSig && logs[i].topics[1] == expectedBanditTopic
                    && logs[i].topics[2] == expectedClanTopic
            ) {
                return;
            }
        }
        fail("expected BanditAttackResolved log");
    }

    function _activateTargetDefenders(uint32 targetClanId, uint32[] memory defenderClanIds, uint256 countEach)
        internal
    {
        uint64 maxExecutesAtTick;
        for (uint256 i = 0; i < defenderClanIds.length; i++) {
            uint64 executesAtTick = _submitTargetDefenders(defenderClanIds[i], targetClanId, countEach);
            if (executesAtTick > maxExecutesAtTick) {
                maxExecutesAtTick = executesAtTick;
            }
        }

        _advanceUntilAfter(maxExecutesAtTick);
        for (uint256 i = 0; i < defenderClanIds.length; i++) {
            world.settleClan(defenderClanIds[i]);
        }
    }

    function _forceAttack(uint32 clanId, uint32 strength) internal returns (uint32 banditId) {
        Clan memory clan = world.getClan(clanId);
        return world.forceAttackingBandit(clan.baseRegion, strength, clanId);
    }

    function _mintClans(uint256 count) internal returns (uint32[] memory clanIds) {
        clanIds = new uint32[](count);
        for (uint256 i = 0; i < count; i++) {
            clanIds[i] = _mintClan();
        }
    }

    function test_defeatedBanditLootDropsHalfCarryAcrossContributingDefenders() public {
        uint32 clanId = _mintClan();
        uint32[] memory defenders = new uint32[](1);
        defenders[0] = clanId;
        _activateTargetDefenders(clanId, defenders, 4);

        Clan memory beforeClan = world.getClan(clanId);
        uint32 banditId = _forceAttack(clanId, 1);
        world.setBanditCarry(banditId, 8e18, 8e18, 8e18, 8e18, 8e18);
        world.setBanditStrength(banditId, 0);

        _advanceTick();

        Clan memory afterClan = world.getClan(clanId);
        assertEq(afterClan.vaultWood, beforeClan.vaultWood, "wood stays in defender carry");
        assertEq(afterClan.vaultWheat, beforeClan.vaultWheat - 4e18, "wheat pays heartbeat upkeep");
        assertEq(afterClan.vaultFish, beforeClan.vaultFish - 4e17, "fish pays heartbeat upkeep");
        assertEq(afterClan.vaultIron, beforeClan.vaultIron, "iron stays in defender carry");
        assertEq(afterClan.goldBalance, beforeClan.goldBalance + 5e18, "gold half carry plus defeat reward");
        for (uint256 i = 0; i < 4; i++) {
            assertEq(world.getClansman(_csId(clanId, i)).carryWood, 1e18, "wood defender share");
            assertEq(world.getClansman(_csId(clanId, i)).carryWheat, 1e18, "wheat defender share");
            assertEq(world.getClansman(_csId(clanId, i)).carryFish, 1e18, "fish defender share");
            assertEq(world.getClansman(_csId(clanId, i)).carryIron, 1e18, "iron defender share");
        }
        assertEq(world.getBandit(banditId).id, 0, "defeated bandit deleted");
    }

    function test_defeatedBanditAwardsBlueprintToTargetClan() public {
        uint32 clanId = _mintClan();
        uint32[] memory defenders = new uint32[](1);
        defenders[0] = clanId;
        _activateTargetDefenders(clanId, defenders, 4);

        uint256 blueprintBefore = world.getClan(clanId).blueprintBalance;
        uint32 banditId = _forceAttack(clanId, 1);
        world.setBanditStrength(banditId, 0);

        vm.expectEmit(true, true, false, true, address(world));
        emit BlueprintEarned(clanId, banditId, world.blueprintUnit(), world.getWorldState().currentTick);

        _advanceTick();

        assertEq(
            world.getClan(clanId).blueprintBalance, blueprintBefore + world.blueprintUnit(), "target blueprint awarded"
        );
    }

    function test_e2e_banditLifecycle_throughHeartbeat() public {
        uint32 clanId = _mintClan();

        uint32 banditId;
        for (uint256 i = 0; i < 64; i++) {
            vm.prevrandao(keccak256(abi.encodePacked("bandit-lifecycle", i)));
            _advanceTick();
            banditId = world.getWorldState().activeBanditId;
            if (banditId != 0) break;
        }
        assertGt(banditId, 0, "bandit spawned through heartbeat");

        _advanceTick();
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Camped), "spawned to camped");

        uint64 campedAt = world.getBandit(banditId).tickEnteredState;
        while (world.getWorldState().currentTick < campedAt + ClanWorldConstants.BANDIT_CAMP_TICKS) {
            _advanceTick();
        }

        vm.recordLogs();
        _advanceTick();
        Vm.Log[] memory logs = vm.getRecordedLogs();
        _assertBanditAttackResolvedLog(logs, banditId, clanId);

        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "attack resolved to resting");

        for (uint64 i = 0; i < ClanWorldConstants.BANDIT_REST_TICKS; i++) {
            _advanceTick();
        }
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Camped), "resting recovered to camped");
        assertEq(world.getBandit(banditId).region, ClanWorldConstants.REGION_MOUNTAINS, "rampaged after rest");
    }

    function test_deadTargetCleanupReleasesDefendersAndEscapesBandit() public {
        uint64 winterStart = world.getWorldState().winterStartsAtTick;
        _advanceUntil(winterStart + 5);

        uint32[] memory clanIds = _mintClans(3);
        world.blockBanditSpawnsForTest();

        uint32 defenderClanId = clanIds[0];
        uint32 targetClanId = clanIds[1];
        uint32 unaffectedClanId = clanIds[2];

        uint64 defenderExecutesAtTick = _submitTargetDefenders(defenderClanId, targetClanId, 1);
        _advanceUntilAfter(defenderExecutesAtTick);
        world.settleClan(defenderClanId);

        uint32 defenderId = _csId(defenderClanId, 0);
        ClansmanState defenderStateBefore = world.getClansman(defenderId).state;
        uint64 defenderCooldownBefore = world.getClansman(defenderId).cooldownEndsAtTs;
        assertEq(uint8(defenderStateBefore), uint8(ClansmanState.ACTING), "defender active before target death");
        assertEq(world.getActiveDefenders(targetClanId).length, 1, "target has active defender");

        uint64 deathFromTick = winterStart;
        world.setClanUpkeepState(targetClanId, deathFromTick, 0, 0);
        world.setLivingClansmenForTest(targetClanId, 3);

        Clan memory unaffectedBefore = world.getClan(unaffectedClanId);
        uint32 banditId = _forceAttack(targetClanId, 100);
        // closedTick semantics (#328b): _abortBanditAttacksForDeadTarget now uses the
        // caller-provided settlement tick (when the last clansman dies), not _world.currentTick.
        // Starvation starts on the tick after upkeep failure; with 3 living clansmen,
        // deaths land at winterStart+2, +3, and +4.
        uint64 expectedBanditAbortTick = deathFromTick + 4;

        vm.recordLogs();
        world.settleClan(targetClanId);
        Vm.Log[] memory logs = vm.getRecordedLogs();
        _assertBanditTargetDiedLog(logs, banditId, targetClanId, expectedBanditAbortTick);

        Clan memory targetAfter = world.getClan(targetClanId);
        assertEq(uint8(targetAfter.clanState), uint8(ClanState.DEAD), "target clan dead");
        assertEq(targetAfter.livingClansmen, 0, "target living count");

        ClansmanState defenderStateAfter = world.getClansman(defenderId).state;
        assertEq(uint8(defenderStateAfter), uint8(ClansmanState.WAITING), "defender released to waiting");
        assertEq(world.getClansman(defenderId).cooldownEndsAtTs, defenderCooldownBefore, "cooldown unchanged");
        assertFalse(world.getActiveMission(defenderId).active, "defender mission cleared");
        assertEq(world.getActiveDefenders(targetClanId).length, 0, "target defender registry cleared");

        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Escaped), "bandit escaped");
        assertEq(world.getBandit(banditId).targetClanId, 0, "bandit target cleared");

        Clan memory unaffectedAfter = world.getClan(unaffectedClanId);
        assertEq(uint8(unaffectedAfter.clanState), uint8(unaffectedBefore.clanState), "clan C state unaffected");
        assertEq(unaffectedAfter.livingClansmen, unaffectedBefore.livingClansmen, "clan C living unaffected");
        assertEq(unaffectedAfter.vaultWheat, unaffectedBefore.vaultWheat, "clan C wheat unaffected");
        assertEq(unaffectedAfter.vaultFish, unaffectedBefore.vaultFish, "clan C fish unaffected");
    }

    function test_defeatedBanditLootSplitsEquallyAcrossContributingClansmen() public {
        uint32[] memory clanIds = _mintClans(4);
        _activateTargetDefenders(clanIds[0], clanIds, 1);

        uint256[4] memory woodBefore;
        uint256[4] memory wheatBefore;
        uint256[4] memory fishBefore;
        uint256[4] memory ironBefore;
        uint256[4] memory goldBefore;
        for (uint256 i = 0; i < clanIds.length; i++) {
            Clan memory clan = world.getClan(clanIds[i]);
            woodBefore[i] = clan.vaultWood;
            wheatBefore[i] = clan.vaultWheat;
            fishBefore[i] = clan.vaultFish;
            ironBefore[i] = clan.vaultIron;
            goldBefore[i] = clan.goldBalance;
        }

        uint32 banditId = _forceAttack(clanIds[0], 1);
        world.setBanditCarry(banditId, 100e18, 100e18, 100e18, 100e18, 100e18);
        world.setBanditStrength(banditId, 0);

        _advanceTick();

        for (uint256 i = 0; i < clanIds.length; i++) {
            Clan memory clan = world.getClan(clanIds[i]);
            assertEq(clan.vaultWood, woodBefore[i], "wood stays out of vault");
            assertEq(
                clan.vaultWheat,
                wheatBefore[i] > 4e18 ? wheatBefore[i] - 4e18 : 0,
                "wheat pays heartbeat upkeep"
            );
            assertEq(
                clan.vaultFish,
                fishBefore[i] > 4e17 ? fishBefore[i] - 4e17 : 0,
                "fish pays heartbeat upkeep"
            );
            assertEq(clan.vaultIron, ironBefore[i], "iron stays out of vault");
            assertEq(clan.goldBalance, goldBefore[i] + (i == 0 ? 29e18 : 7e18), "gold share");
        }
        for (uint256 i = 0; i < clanIds.length; i++) {
            assertEq(world.getClansman(_csId(clanIds[i], 0)).carryWood, 7e18, "explicit wood share");
        }
        assertEq(world.getClansman(_csId(clanIds[0], 1)).carryWood, 7e18, "waiting wood share 1");
        assertEq(world.getClansman(_csId(clanIds[0], 2)).carryWood, 7e18, "waiting wood share 2");
        assertEq(world.getClansman(_csId(clanIds[0], 3)).carryWood, 7e18, "waiting wood share 3");
        for (uint256 i = 0; i < clanIds.length; i++) {
            assertEq(world.getClansman(_csId(clanIds[i], 0)).carryIron, 5e18, "iron capped");
        }
    }

    function test_mixedClanDefenseAwardsBlueprintOnlyToBaseOwner() public {
        uint32[] memory clanIds = _mintClans(4);
        _activateTargetDefenders(clanIds[0], clanIds, 1);

        uint256[4] memory blueprintBefore;
        for (uint256 i = 0; i < clanIds.length; i++) {
            blueprintBefore[i] = world.getClan(clanIds[i]).blueprintBalance;
        }

        uint32 banditId = _forceAttack(clanIds[0], 1);
        world.setBanditStrength(banditId, 0);

        _advanceTick();

        assertEq(
            world.getClan(clanIds[0]).blueprintBalance,
            blueprintBefore[0] + world.blueprintUnit(),
            "base owner blueprint"
        );
        for (uint256 i = 1; i < clanIds.length; i++) {
            assertEq(world.getClan(clanIds[i]).blueprintBalance, blueprintBefore[i], "helper clan no blueprint");
        }
    }

    function test_multipleDefeatedBanditsEachAwardBlueprint() public {
        uint32 clanId = _mintClan();
        uint32[] memory defenders = new uint32[](1);
        defenders[0] = clanId;
        _activateTargetDefenders(clanId, defenders, 4);

        uint256 blueprintBefore = world.getClan(clanId).blueprintBalance;
        uint32 firstBanditId = _forceAttack(clanId, 1);
        uint32 secondBanditId = _forceAttack(clanId, 1);
        world.setBanditStrength(firstBanditId, 0);
        world.setBanditStrength(secondBanditId, 0);

        _advanceTick();

        assertEq(
            world.getClan(clanId).blueprintBalance,
            blueprintBefore + 2 * world.blueprintUnit(),
            "one blueprint per defeated bandit"
        );
    }

    function test_waitingAtHomeClansmenContributePassiveDefense() public {
        uint32 clanId = _mintClan();
        uint256 blueprintBefore = world.getClan(clanId).blueprintBalance;
        uint32 banditId = _forceAttack(clanId, 10);

        _advanceTick();

        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.None), "passive defenders defeated bandit");
        assertEq(world.getClan(clanId).blueprintBalance, blueprintBefore + world.blueprintUnit(), "blueprint awarded");
        assertEq(world.getClan(clanId).livingClansmen, 4, "no casualties");
    }

    function test_failedDefenseStealsTwentyPercentAndAccumulatesCarry() public {
        uint32 clanId = _mintClan();
        uint32 banditId = _forceAttack(clanId, 100);
        world.setBanditCarry(banditId, 1e18, 2e18, 3e18, 4e18, 0);

        _advanceTick();

        Clan memory clan = world.getClan(clanId);
        assertEq(clan.vaultWood, 16e18, "wood drained");
        assertEq(clan.vaultWheat, 128e17, "wheat upkeep then drained");
        assertEq(clan.vaultFish, 128e16, "fish upkeep then drained");

        assertEq(world.getBandit(banditId).carryWood, 5e18, "wood carry accumulated");
        assertEq(world.getBandit(banditId).carryWheat, 52e17, "wheat carry accumulated after upkeep");
        assertEq(world.getBandit(banditId).carryFish, 332e16, "fish carry accumulated after upkeep");
        assertEq(world.getBandit(banditId).carryIron, 4e18, "iron carry unchanged without vault iron");
    }

    function test_winterStarvationReplayUsesHistoricalWinterTicks() public {
        uint64 winterStart = world.getWorldState().winterStartsAtTick;
        _advanceUntil(winterStart + 30);
        assertFalse(world.getWorldState().winterActive, "test settles after winter");

        uint32 clanId = _mintClan();
        world.setClanUpkeepState(clanId, winterStart, 0, 0);

        ClanFullView memory preview = world.getClanFullView(clanId);
        assertEq(uint8(preview.clan.clan.clanState), uint8(ClanState.DEAD), "derived view replays winter deaths");
        assertEq(preview.clan.clan.livingClansmen, 0, "derived living count");

        world.settleClan(clanId);

        Clan memory settled = world.getClan(clanId);
        assertEq(uint8(settled.clanState), uint8(ClanState.DEAD), "settlement replays winter deaths");
        assertEq(settled.livingClansmen, 0, "settled living count");
    }

    function test_resolveBanditAttackReturnsWhenTargetDiesDuringSettlement() public {
        uint64 winterStart = world.getWorldState().winterStartsAtTick;
        _advanceUntil(winterStart + 3);

        uint32 targetClanId = _mintClan();
        world.setClanUpkeepState(targetClanId, winterStart, 0, 0);
        world.setLivingClansmenForTest(targetClanId, 1);

        uint32 banditId = _forceAttack(targetClanId, 100);

        _advanceTick();

        assertEq(uint8(world.getClan(targetClanId).clanState), uint8(ClanState.DEAD), "target starved");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Escaped), "bandit escaped once");
        assertEq(world.getBandit(banditId).targetClanId, 0, "bandit target cleared");
    }

    function test_banditAttackDefersWhenTargetStillStaleAfterCappedSettlement() public {
        _advanceUntil(251);

        uint32 targetClanId = _mintClan();
        uint64 attackTick = world.getWorldState().currentTick;
        world.setClanUpkeepState(targetClanId, attackTick - 250, 1000e18, 100e18);

        Clan memory beforeClan = world.getClan(targetClanId);
        uint32 banditId = _forceAttack(targetClanId, 100);

        _advanceTick();

        Clan memory afterClan = world.getClan(targetClanId);
        assertEq(afterClan.lastSettledTick, attackTick - 50, "settlement capped before attack tick");
        assertEq(afterClan.wallLevel, beforeClan.wallLevel, "no wall damage");
        assertEq(uint8(afterClan.clanState), uint8(ClanState.ACTIVE), "target remains alive");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "bandit deferred to resting");
        assertEq(world.getBandit(banditId).targetClanId, 0, "target cleared for later pick");
        assertEq(world.getBandit(banditId).attackAttemptsMade, 0, "deferred attack not counted");
        assertEq(world.getBandit(banditId).carryWood, 0, "bandit carry unchanged");
    }

    function test_defeatedBanditLootBurnsDropRemainderAndCarryOverflow() public {
        uint32[] memory clanIds = _mintClans(3);
        _activateTargetDefenders(clanIds[0], clanIds, 1);

        uint256 goldBeforeTotal;
        for (uint256 i = 0; i < clanIds.length; i++) {
            Clan memory clan = world.getClan(clanIds[i]);
            goldBeforeTotal += clan.goldBalance;
        }

        uint32 banditId = _forceAttack(clanIds[0], 1);
        world.setBanditCarry(banditId, 100e18, 100e18, 100e18, 100e18, 100e18);
        world.setBanditStrength(banditId, 0);

        vm.expectEmit(true, true, false, false, address(world));
        emit BanditAttackResolved(banditId, clanIds[0], true, 0, 0, 0, 0, 0, 0, 0, world.getWorldState().currentTick);
        vm.expectEmit(true, true, false, true, address(world));
        emit BanditDefeated(banditId, clanIds[0], world.getWorldState().currentTick);

        _advanceTick();

        uint256 goldAfterTotal;
        for (uint256 i = 0; i < clanIds.length; i++) {
            Clan memory clan = world.getClan(clanIds[i]);
            assertEq(clan.goldBalance, 3e18 + (i == 0 ? 33e18 : 8e18), "gold share");
            goldAfterTotal += clan.goldBalance;
        }
        assertEq(goldAfterTotal - goldBeforeTotal, 49e18, "undropped gold plus defeat reward");
        assertEq(world.getClansman(_csId(clanIds[0], 0)).carryWood, 8e18, "target explicit wood");
        assertEq(world.getClansman(_csId(clanIds[0], 1)).carryIron, 5e18, "target waiting iron capped");
        assertEq(world.getClansman(_csId(clanIds[1], 0)).carryFish, 8e18, "helper fish capped");
    }

    function test_multipleDefendersFromSameClanEachReceiveDefenderShare() public {
        uint32[] memory clanIds = _mintClans(2);
        uint64 firstExecutesAtTick = _submitTargetDefenders(clanIds[0], clanIds[0], 3);
        uint64 secondExecutesAtTick = _submitTargetDefenders(clanIds[1], clanIds[0], 1);
        _advanceUntilAfter(firstExecutesAtTick > secondExecutesAtTick ? firstExecutesAtTick : secondExecutesAtTick);
        world.settleClan(clanIds[0]);
        world.settleClan(clanIds[1]);

        uint256 targetWoodBefore = world.getClan(clanIds[0]).vaultWood;
        uint256 helperWoodBefore = world.getClan(clanIds[1]).vaultWood;
        uint32 banditId = _forceAttack(clanIds[0], 1);
        world.setBanditCarry(banditId, 100e18, 100e18, 100e18, 100e18, 100e18);
        world.setBanditStrength(banditId, 0);

        _advanceTick();

        assertEq(world.getClan(clanIds[0]).vaultWood, targetWoodBefore, "target vault unchanged");
        assertEq(world.getClan(clanIds[1]).vaultWood, helperWoodBefore, "helper vault unchanged");
        for (uint256 i = 0; i < 4; i++) {
            assertEq(world.getClansman(_csId(clanIds[0], i)).carryWood, 10e18, "target defender share");
        }
        assertEq(world.getClansman(_csId(clanIds[1], 0)).carryWood, 10e18, "helper defender share");
    }

    function test_failedDefenseStealsVaultLootAndKeepsCarryWhileResting() public {
        uint32 clanId = _mintClan();
        Clan memory beforeClan = world.getClan(clanId);
        uint32 banditId = _forceAttack(clanId, 100);

        _advanceTick();

        Clan memory afterClan = world.getClan(clanId);
        assertEq(afterClan.vaultWood, beforeClan.vaultWood - 4e18, "wood stolen");
        assertEq(afterClan.vaultWheat, 128e17, "wheat upkeep then stolen");
        assertEq(afterClan.vaultFish, 128e16, "fish upkeep then stolen");
        assertEq(afterClan.vaultIron, beforeClan.vaultIron, "iron unchanged");
        assertEq(afterClan.goldBalance, beforeClan.goldBalance, "gold unchanged");
        assertEq(world.getBandit(banditId).carryWood, 4e18, "bandit carry wood");
        assertEq(world.getBandit(banditId).carryWheat, 32e17, "bandit carry wheat after upkeep");
        assertEq(world.getBandit(banditId).carryFish, 32e16, "bandit carry fish after upkeep");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "bandit resting");
    }

    function test_escapedBanditDoesNotAwardBlueprint() public {
        uint32 clanId = _mintClan();
        uint256 blueprintBefore = world.getClan(clanId).blueprintBalance;
        uint32 banditId = _forceAttack(clanId, 100);

        _advanceTick();

        assertEq(world.getClan(clanId).blueprintBalance, blueprintBefore, "blueprint unchanged");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "bandit resting");
    }

    function test_sixthFailedAttackTerminallyEscapesAndBurnsCarry() public {
        uint32 clanId = _mintClan();
        uint32 banditId = _forceAttack(clanId, 100);
        world.setBanditCarry(banditId, 100e18, 100e18, 100e18, 100e18, 100e18);

        for (uint8 attempt = 1; attempt <= ClanWorldConstants.BANDIT_MAX_ATTACK_ATTEMPTS; attempt++) {
            world.setWallLevel(clanId, 1);
            world.forceBanditAttackAttempt(banditId, clanId);
            _advanceTick();

            if (attempt < ClanWorldConstants.BANDIT_MAX_ATTACK_ATTEMPTS) {
                assertEq(world.getBandit(banditId).id, banditId, "bandit remains before cap");
                assertEq(world.getBandit(banditId).attackAttemptsMade, attempt, "attempt counted");
                assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "nonterminal rest");
            }
        }

        assertEq(world.getBandit(banditId).id, 0, "bandit deleted at attempt cap");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.None), "terminal escape removed troop");
        assertEq(world.getBandit(banditId).carryWood, 0, "carry burned with deletion");
    }

    function test_twoAliveDefendersWithSufficientDefenseDefeatBanditWithoutWallChip() public {
        uint32 clanId = _mintClan();
        _submitDefenders(clanId, 2);
        world.setWallLevel(clanId, 1);

        uint32 banditId = _forceAttack(clanId, 1);
        world.setBanditStrength(banditId, 15);
        _advanceTick();

        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.None), "defeated bandit deleted");
        assertEq(world.getClan(clanId).wallLevel, 1, "wall was not chipped");
        assertEq(world.getClan(clanId).livingClansmen, 4, "no casualties");
    }

    function test_weakDefenseChipsWallOneLevel() public {
        uint32 clanId = _mintClan();
        world.setWallLevel(clanId, 1);
        uint32 banditId = _forceAttack(clanId, 100);
        world.setBanditStrength(banditId, 130);

        _advanceTick();

        assertEq(world.getClan(clanId).wallLevel, 0, "wall level decreased");
        assertEq(world.getClan(clanId).livingClansmen, 4, "wall absorbed full hit");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "bandit resting");
    }

    function test_wallZeroWeakDefenseKillsClansmanDeterministically() public {
        uint32 clanId = _mintClan();
        uint32 banditId = _forceAttack(clanId, 100);

        _advanceTick();

        assertEq(world.getClan(clanId).livingClansmen, 3, "one clansman died");
        assertEq(uint8(world.getBandit(banditId).state), uint8(BanditState.Resting), "bandit resting");
    }

    function test_allClansmenDeadMarksClanDead() public {
        uint32 clanId = _mintClan();
        _forceAttack(clanId, 425);

        _advanceTick();

        Clan memory clan = world.getClan(clanId);
        assertEq(clan.livingClansmen, 0, "all clansmen dead");
        assertEq(uint8(clan.clanState), uint8(ClanState.DEAD), "target clan dead");
        assertEq(clan.vaultWood, 0, "dead target wood burned");
        assertEq(clan.vaultWheat, 0, "dead target wheat burned");
        assertEq(clan.vaultFish, 0, "dead target fish burned");
        assertEq(clan.vaultIron, 0, "dead target iron burned");
    }

    function test_starvingDefenderContributesZeroDefense() public {
        uint32 clanId = _mintClan();
        _submitDefenders(clanId, 1);
        _advanceTick();
        world.settleClan(clanId);
        world.setWallLevel(clanId, 1);
        world.setClanUpkeepState(clanId, world.getWorldState().currentTick, 0, 0);
        world.setStarvationStartsAt(clanId, world.getWorldState().currentTick);

        _forceAttack(clanId, 100);

        _advanceTick();

        assertEq(world.getClan(clanId).wallLevel, 0, "starving defender did not reduce incoming wall hit");
        assertEq(world.getClan(clanId).livingClansmen, 4, "wall absorbed full hit");
    }

    function test_twoAttacksSameTickDeterminismAcrossReplay() public {
        BanditAttackHarness a = new BanditAttackHarness();
        BanditAttackHarness b = new BanditAttackHarness();

        uint32 aFirst;
        uint32 aSecond;
        uint32 bFirst;
        uint32 bSecond;
        (aFirst, aSecond) = _setupTwoAttackWorld(a);
        (bFirst, bSecond) = _setupTwoAttackWorld(b);

        vm.warp(block.timestamp + ClanWorldConstants.HEARTBEAT_INTERVAL_SECONDS);
        a.heartbeat();
        b.heartbeat();

        assertEq(a.getClan(aFirst).livingClansmen, b.getClan(bFirst).livingClansmen, "first target deterministic");
        assertEq(a.getClan(aSecond).livingClansmen, b.getClan(bSecond).livingClansmen, "second target deterministic");
        assertEq(uint8(a.getBandit(1).state), uint8(b.getBandit(1).state), "first bandit state deterministic");
        assertEq(uint8(a.getBandit(2).state), uint8(b.getBandit(2).state), "second bandit state deterministic");
    }

    function _setupTwoAttackWorld(BanditAttackHarness target)
        internal
        returns (uint32 firstClanId, uint32 secondClanId)
    {
        vm.prank(elder);
        (firstClanId,) = target.mintClan(elder);
        vm.prank(elder);
        (secondClanId,) = target.mintClan(elder);

        target.forceAttackingBandit(target.getClan(firstClanId).baseRegion, 100, firstClanId);
        target.forceAttackingBandit(target.getClan(secondClanId).baseRegion, 100, secondClanId);
    }
}
